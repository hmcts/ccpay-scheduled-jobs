plugins {
    id 'java'
    id 'java-library'
    id 'jacoco'
    id 'org.owasp.dependencycheck' version '10.0.2'
    id 'org.sonarqube' version '5.1.0.4882'
}

group 'uk.gov.hmcts.reform.payment'


def javaLoggingVersion = '6.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    jcenter()
}


configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails det ->
            /* JAR upgrades with latest versions for CVE fixes*/

            /*   CVE-2020-17521 */
            if (det.requested.name == 'groovy-xml' || det.requested.name == 'groovy-json') {
                det.useVersion '3.0.7'
            }
            /* CVE-2020-13956 */
            if (det.requested.name == 'httpclient') {
                det.useVersion '4.5.13'
            }

            /* CVE-2021-29425 */
            if (det.requested.name == 'commons-io') {
                det.useVersion '2.8.0'
            }
            
            /*CVE-2021-44228, CVE-2021-44832, CVE-2021-45046, CVE-2021-45105*/
            if (det.requested.name == 'log4j-api' || det.requested.name == 'log4j-to-slf4j') {
                det.useVersion '2.17.1'
            }

            /*CVE-2021-44228, CVE-2021-44832, CVE-2021-45046, CVE-2021-45105*/
            if (det.requested.name == 'log4j-api' || det.requested.name == 'log4j-to-slf4j') {
                det.useVersion '2.17.1'
            }

            // Latest Spring libs. 04/08/2023
            if (det.requested.name == 'spring-aop'||det.requested.name == 'spring-aspects'||det.requested.name == 'spring-beans'
                    ||det.requested.name == 'spring-context'||det.requested.name == 'spring-core'||det.requested.name == 'spring-expression'
                    ||det.requested.name == 'spring-jcl'||det.requested.name == 'spring-jdbc'||det.requested.name == 'spring-orm'
                    ||det.requested.name == 'spring-tx'||det.requested.name == 'spring-web'||det.requested.name == 'spring-webmvc'
                    ||det.requested.name == 'spring-context-support'||det.requested.name == 'spring-test'
                    ||det.requested.name == 'spring-web'||det.requested.name == 'spring-aop'||det.requested.name == 'spring-context'
                    ||det.requested.name == 'spring-expression'
            ) {
                det.useVersion '6.1.6'
            }

            /* CVE CVE-2022-41854 */
            if (det.requested.name == 'snakeyaml') {
                det.useVersion '2.2'
            }

        }
    }
}

dependencies {
    implementation group: 'io.rest-assured', name: 'rest-assured', version: '4.5.1'

    implementation(group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.1.3') {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'org.springframework.security', module: 'spring-security-crypto'
    }
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk18on', version: '1.78.1'
    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.1.5'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: '3.3.1'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.1.2'
    implementation group: 'io.github.openfeign', name: 'feign-jackson', version: '13.3'
    implementation group: 'org.json', name: 'json', version: '20240303'

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.10.3")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.10.3")
    testImplementation 'com.github.tomakehurst:wiremock:3.0.1'
}

jacocoTestReport {
  executionData(test)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

test {
    useJUnitPlatform()
}

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: payment-jobs"
    property "sonar.projectKey", "uk.gov.hmcts.reform:payment-jobs"
    property "sonar.coverage.exclusions", "**/JobProcessorConfiguration*,**JobProcessorApplication*"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
  }
}


dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

jar {
    manifest {
        attributes  'Main-Class': 'JobProcessorApplication', 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
}

tasks.withType(Copy) {
    filesMatching("**/*.yaml") {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}

task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.compileClasspath
}
